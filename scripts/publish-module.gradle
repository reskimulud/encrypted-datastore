apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Extention Function to Encrypt and Decrypt DataStore with AES-256-CBC'
                    url = 'https://github.com/reskimulud/encrypted-datastore'
                    inceptionYear = '2022'

                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://github.com/reskimulud/encrypted-datastore/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'reskimulud'
                            name = 'Reski Mulud Muchamad'
                            email = 'reski.mulud@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git:github.com/reskimulud/encrypted-datastore.git'
                        developerConnection = 'scm:git:ssh://github.com/reskimulud/encrypted-datastore.git'
                        url = 'https://github.com/reskimulud/encrypted-datastore'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}